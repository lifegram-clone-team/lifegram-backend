plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'jacoco'
}

group = 'team.five'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

jar{
    enabled = false
}

repositories {
    mavenCentral()
}

dependencies {

    // Spring Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // JPA
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JWT
    compileOnly group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // S3 ImageUpload
    implementation 'io.awspring.cloud:spring-cloud-starter-aws:2.3.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

processResources.dependsOn('securityInfo')

task securityInfo() {
    copy {
        from './lifegram-backend-security/application.properties'
        include '*.properties'
        into './src/main/resources'
    }

    copy {
        from './lifegram-backend-security'
        include 'keystore.p12'
        into './src/main/resources/ssl'
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = layout.buildDirectory.dir("$buildDir/reports/jacoco")
}

jacocoTestReport {
    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude:  [
                    "**/*Controller*",
                    "**/*Config*",
                    "**/*LifegramApplication*",
                    "**/*Builder*",
                    "**/*Exception*"
            ])
        })
    }

    reports {
        xml.required = false;
        csv.required = false;
        html.outputLocation = layout.buildDirectory.dir("$buildDir/reports/html-report")
    }

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = "CLASS"

            limit {
                counter = "INSTRUCTION"
                value = "COVEREDRATIO"
                minimum = 0.80
            }

            limit {
                counter = "BRANCH"
                value = "COVEREDRATIO"
                minimum = 0.80
            }

            excludes = [
                    "**.*Builder",
                    "**.LifegramApplication",
                    "**.*Config",
                    "**.*Controller",
                    "**.*Exception*"
            ]
        }
    }
}